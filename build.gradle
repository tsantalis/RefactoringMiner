apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'

apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'idea'


group = "com.github.tsantalis"
version = "3.0.4"

applicationName = rootProject.name
mainClassName = "org.refactoringminer.RefactoringMiner"

idea {
    module {
        excludeDirs.add(file("src-test/data/astDiff/"))
        excludeDirs.add(file("tmp"))
    }
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

dependencies {
    implementation 'org.eclipse.jgit:org.eclipse.jgit:6.8.0.202311291450-r'
    implementation 'org.slf4j:slf4j-api:2.0.11'
    implementation 'org.slf4j:slf4j-simple:2.0.11'
    implementation 'org.eclipse.jdt:org.eclipse.jdt.core:3.36.0'
    implementation 'org.apache.commons:commons-text:1.11.0'
    implementation 'org.kohsuke:github-api:1.135'
    implementation 'io.github.java-diff-utils:java-diff-utils:4.12'
	implementation 'com.github.gumtreediff:core:3.0.0'
	implementation 'com.github.gumtreediff:gen.jdt:3.0.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.0'
    testImplementation 'net.joshka:junit-json-params:5.9.2-r0'
    testImplementation 'org.skyscreamer:jsonassert:1.5.1'
    testImplementation 'org.glassfish:javax.json:1.1.4'
    testImplementation "org.jcommander:jcommander:1.83"
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
}

java {
    withJavadocJar()
    withSourcesJar()
}

artifacts {
    archives javadocJar, sourcesJar
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

publishing {
    repositories {
        maven {
            def releaseRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo : snapshotRepo
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId = 'refactoring-miner'
            from components.java

            pom {
                name = "Refactoring Miner"
                description = 'RefactoringMiner is a library/API written in Java that can detect refactorings applied in the history of a Java project.'
                url = 'https://github.com/tsantalis/RefactoringMiner'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'tsantalis'
                        name = 'Nikolaos Tsantalis'
                        email = 'tsantalis@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:tsantalis/RefactoringMiner.git'
                    developerConnection = 'scm:git:git@github.com:tsantalis/RefactoringMiner.git'
                    url = 'https://github.com/tsantalis/RefactoringMiner/tree/master'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

jar {
    manifest {
        attributes(
            'Main-Class': 'org.refactoringminer.RefactoringMiner'
        )
    }
}
